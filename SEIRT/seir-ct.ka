///////////////////////////////////////////////
//// Agents, or entities in the simulation ////
///////////////////////////////////////////////

// The internal states of the individual person agents:
//  - s{s e i r}           : infection state
//  - d{u d}               : diagnostic state - undiagnosed or diagnosed
%agent: P(s{s e i r}, d{u d})

// Tracing agent, virtual particles that assist the simulation with
// parallel internal state
%agent: C(s{s e i r})

///////////////
//// RULES ////
///////////////

//////////////////////////////////////////////
// Interactions with infectious individuals //
//////////////////////////////////////////////

'lucky'    P(s{s}, d{u}), P(s{i}, d{u}), . -> P(s{s}, d{u}), P(s{i}, d{u}), C(s{s}) @ (1-beta)*c/N
'exposure' P(s{s}, d{u}), P(s{i}, d{u}), . -> P(s{e}, d{u}), P(s{i}, d{u}), C(s{e}) @ beta*c/N
'exposed'  P(s{e}, d{u}), P(s{i}, d{u}), . -> P(s{e}, d{u}), P(s{i}, d{u}), C(s{e}) @ c/N
'infected' P(s{i}, d{u}), P(s{i}, d{u}), . -> P(s{i}, d{u}), P(s{i}, d{u}), C(s{i}) @ c/N
'immune'   P(s{r}, d{u}), P(s{i}, d{u}), . -> P(s{r}, d{u}), P(s{i}, d{u}), C(s{r}) @ c/N

////////////////////////////////////////
// Disease progression, post-exposure //
////////////////////////////////////////
'progression'   P(s{e/i}) @ alpha
'c_progression' C(s{e/i}) @ alpha

'recovery'      P(s{i/r}) @ gamma
'c_recovery'    C(s{i/r}) @ gamma

///////////////////////////////
// Diagnosis through testing //
///////////////////////////////
'testing'       P(s{i}, d{u/d}) @ theta

//////////////////////////////////////////////////////////////////////////////////////////
// Trace individuals in a biased way. Exposed and  Infected individuals are more likely //
// to be traced. Tracing is a kind of presumptive diagnosis.                            //
//                                                                                      //
// Tracing for E and I individuals is different from tracing for S and R individuals.   //
// For E and I, we know they must have been in contact with an I in in the past. For S  //
// and R, their probability of being traced depends on the number of Is in circulation  //
//////////////////////////////////////////////////////////////////////////////////////////
'tracing_S'     P(s{s}, d{u/d}), P(s{i}, d{d}), C(s{s}) @ theta*chi*eta/N
'tracing_E'     P(s{e}, d{u/d}), P(s{i}, d{d}), C(s{e}) @ theta*chi*eta/N
'tracing_I'     P(s{i}, d{u/d}), P(s{i}, d{d}), C(s{i}) @ theta*chi*eta/N
'tracing_R'     P(s{r}, d{u/d}), P(s{i}, d{d}), C(s{r}) @ theta*chi*eta/N

///////////////////////
// Leaving isolation //
///////////////////////
'exit_S'        P(s{s}, d{d/u}) @ kappa
'exit_R'        P(s{r}, d{d/u}) @ kappa

//////////////////////////////////////
// Degradation of tracing particles //
//////////////////////////////////////
'degradation'   C() -> . @ gamma + theta*(1 + chi*eta)

/////////////////////
//// Observables ////
/////////////////////
%obs: 'US'   |P(s{s}, d{u})|        // Susceptible
%obs: 'DS'   |P(s{s}, d{d})|        // Susceptible
%obs: 'UE'   |P(s{e}, d{u})|        // Exposed
%obs: 'DE'   |P(s{e}, d{d})|        // Exposed
%obs: 'UI'   |P(s{i}, d{u})|        // Infected
%obs: 'DI'   |P(s{i}, d{d})|        // Infected
%obs: 'UR'   |P(s{r}, d{u})|        // Recovered
%obs: 'DR'   |P(s{r}, d{d})|        // Recovered

////////////////////////
//// Initialisation ////
////////////////////////
%init: Init_S  P(s{s}, d{u})
%init: Init_I  P(s{i}, d{u})

///////////////////////
//// Perturbations ////
///////////////////////

// stop the simulation after 300 days
%mod: alarm 500 do $STOP;
